//The DSL Rules for Q7
/* Q7 stands for 7 kinds of questions one ask in eliciting requirements:
   Why and How are used to eliciting goals
   Who is used to eliciting actors
   How much is used to eliciting contributions to softgoals
   What is used to eliciting subject matters or topics
   When is used to eliciting contexts
   Where is used to eliciting aspects 
*/
/*
Model: (advices+=Advice)+ ;
*/
Advice: 
  (who='<' Who '>' '::')?
  (when=When "()=>")? 
  why=Why 
  ('[' what=What ']')? 
  ("<=" where=Where )? 
  ('{' how=How '}')?  
 ((label=Label)? ("=>" howmuch=HowMuchRules | '==>' '<' whom=Who '>'))?;
Who: actor=Actor;
Actor: name=STRING | name=ID;
When: expr='true' | expr='false'; // | expr=ID | expr=STRING;
Why: name=ID | name=STRING;
What: topics+=Topic (',' topics+=Topic)*;  
Topic: name=ID | name=STRING;
Where: op=Op name=Why ('[' topics=What ']')?;
How: op=DecompositionType (enrich=Enrichment)? (advices+=Advice)+;
Enum Op: 
    HELP='+' | HELP2="HELP" 
  | HURT='-' | HURT2="HURT"
  | MAKE="++"| MAKE2="MAKE" 
  | BREAK="--"|BREAK2="BREAK";
Enum DecompositionType: 
    AND='AND' | AND1='&' 
  | OR='OR' | OR1='|';
Enum Enrichment: 
    SEQUENTIAL='SEQ' | SEQUENTIAL1=';' 
  | PARALLEL='PAR' | PARALLEL1='||' 
  | XOR='XOR' | XOR1='/';
HowMuchRules: howmuch+=HowMuch (',' howmuch+=HowMuch)*;
HowMuch: op=Op (strength=FLOAT)? ('<' who=Who '>' '::')? why=Why ('[' what=What ']')? 
  | '~' (trust=FLOAT)? '<' who=Who '>' ('::' why=Why ('[' what=What ']'))?
  ;   
Label: label=LabelEnumerator 
  | (sat=FLOAT ',' den=FLOAT);
Enum LabelEnumerator: 
    FULLY_SATISFIED ='FS' 
  | PARTIALLY_SATISFIED='PS' 
  | FULLY_DENIED='FD' 
  | PARTIALLY_DENIED='PD' 
  | CONFLICT='CF' 
  | UNKNOWN='UN';
  
FLOAT: integral=INT ('.' decimal=INT)?;
